// Main Scilab initialisation file
// Copyright INRIA
mode(-1);  // silent execution mode

// clean database when restarted ======================================
predef('clear'); //unprotect all variables
clear  // erase all variables
clearglobal();

// Set stack size   ===================================================
defaultstacksize=5000000;
old=stacksize()
params=sciargs();
nparam=find(params=='-mem');
if (nparam) then
  ierr=execstr('newstacksize='+params(nparam+1),'errcatch');
  if (ierr==0) then
    if old(1)<>newstacksize then stacksize(newstacksize),end
  else
    if old(1)<>defaultstacksize then stacksize(defaultstacksize),end
  end
else
  if old(1)<>defaultstacksize then stacksize(defaultstacksize),end
end
clear nparam params ierr old newstacksize defaultstacksize
// Special variables definition =======================================
ieee(2);%inf=1/0;ieee(0);%nan=%inf-%inf;
%T=%t;%F=%f;       // boolean variables
// Startup message  ===================================================
verbose=sciargs()<>"-nb"
if verbose then
  if ~fromjava() & ~fromc() then
    banner();
    write(%io(2),[' ';' ';gettext("Startup execution:")])
  end
end
// Load scilab functions libraries  ===================================
if verbose then
  if ~fromjava() & ~fromc() then
    write(%io(2),gettext("  loading initial environment"))
  end
end
clear verbose

// Create some configuration variables ================================
PWD = getcwd()

// Define Initial demo tables  ========================================
// demolist is a two column matrix of strings
global demolist
demolist=[];
clear demolist;
// Set LANGUAGE  ======================================================
// used mainly for on-line help
args=sciargs(); larg=find(args=="-l")
if larg<>[] & larg<=size(args,"*") then
  L=args(larg+1);
  setlanguage(L);
end
clear larg
// loads modules ======================================================

modules=getmodules();
index=size(modules);
for i=1:index(1) do
 exec("SCI/modules/"+modules(i)+"/etc/"+modules(i)+".start",-1);
end

clear modules index i add_demo add_help_chapter add_module_help_chapter
// Create some configuration variables ================================
home=getenv('HOME',SCI);
if MSDOS then
  // path of scilab main directory for Windows
  WSCI=getlongpathname(pathconvert(SCI,%f,%f,'w'))
  clear pathconvert
else
  if getenv('PRINTERS','ndef')=="ndef" then
    setenv("PRINTERS","lp")
  end
end
setenv("VERSION",getversion())


// Set the preferred browser  ==========================================
global %browsehelp
%browsehelp=loaddefaultbrowser();
clear %browsehelp with_tk with_gtk loaddefaultbrowser //remove the local variable

// Menu for Help and editor ===========================================
if grep(args,'lt-scilab-bin')<>[] then
  if (args<>"-nw")&(args<>"-nwni")&(args<>"--texmacs")&(args<>"-nogui") then

    if ~MSDOS then
      delmenu(gettext("Help"))
      addmenu(gettext("Help"),[gettext("Help browser"),gettext("Apropos"),gettext("Configure")],list(2,"help_menu")),
    end
    if with_tk() then
	    if ~MSDOS then
        delmenu(gettext("Editor"))
        addmenu(gettext("Editor"),list(2,"scipad();"));
      end
    end
  end
end
clear ans  %b_h_s args with_tk addmenu addMenuSubMenusCallback addMenuSubMenus addSingleMenuCallback addSingleMenu
// Protect variable previously defined  ================================
predef('all')
// Graphic mode and Startup info ======================================
if %gui then
  verbose=sciargs()<>"-nb"
  if verbose then
	  show_startupinfo();
	  clear show_startupinfo;
  end
  clear verbose;
end
// load contrib menu if present ========================================
[fd,ierr]=mopen(SCI+'/contrib/loader.sce');
if ierr== 0 then;
  mclose(fd);
  global %toolboxes
  global %toolboxes_dir
  exec(SCI+'/contrib/loader.sce');
  clear %toolboxes %toolboxes_dir
end
clear fd ierr

// calling user initialization =========================================
// Home dir startup (if any)
if sciargs()<>'-nouserstartup' then
  if MSDOS then
    startup_path=SCIHOME+'\'
  else
    startup_path=SCIHOME+'/'
  end

  [startup,ierr]=mopen(startup_path+'.scilab','r')
  if ierr==0 then
    exec(startup,-1);mclose(startup)
  else
    [startup,ierr]=mopen(startup_path+'scilab.ini','r')
    if ierr==0 then
      exec(startup,-1);mclose(startup)
    end
  end
  clear startup ierr startup_path

  // working dir startup (if any)
  if  home<>PWD then
    [startup,ierr]=mopen('.scilab','r')
    if ierr==0 then
      exec(startup,-1);mclose(startup)
    else
      [startup,ierr]=mopen('scilab.ini','r')
      if ierr==0 then
	exec(startup,-1);mclose(startup)
      end
    end
  end
end
clear startup ierr
// =====================================================================
