// Main Scilab initifalisation file 
// Copyright INRIA
mode(-1);  // silent execution mode

// clean database when restarted ======================================
predef('clear'); //unprotect all variables 
clear  // erase all variables 
clearglobal();

// Set stack size   ===================================================
defaultstacksize=5000000;
old=stacksize()
params=sciargs();
nparam=find(params=='-mem');
if (nparam) then
  ierr=execstr('newstacksize='+params(nparam+1),'errcatch');
  if (ierr==0) then
    if old(1)<>newstacksize then stacksize(newstacksize),end
  else
    if old(1)<>defaultstacksize then stacksize(defaultstacksize),end
  end
else
  if old(1)<>defaultstacksize then stacksize(defaultstacksize),end
end
clear nparam params ierr old newstacksize defaultstacksize
// Special variables definition =======================================
ieee(2);%inf=1/0;ieee(0);%nan=%inf-%inf;

%s=poly(0,'s');%z=poly(0,'z');
$=poly(0,'$')

%T=%t;%F=%f;       // boolean variables

// Startup message  ===================================================
verbose=sciargs()<>"-nb"
if verbose then
  if ~fromjava() & ~fromc() then
    banner(); 
    write(%io(2),[' ';' ';'Startup execution:'])
  end
end

// Load scilab functions libraries  ===================================
if verbose then 
  if ~fromjava() & ~fromc() then
    write(%io(2),'  loading initial environment')
  end  
end 
clear verbose

// use MSDOS syntax?
MSDOS = (getos() == "Windows")


load('SCI/macros/sci2for/lib')
load('SCI/macros/tdcs/lib')
load('SCI/macros/util/lib')
load('SCI/macros/int/lib')
load('SCI/macros/percent/lib')
// Create some configuration variables ================================
// path of scilab main directory
SCI=getenv('SCI');
TMPDIR=getenv('TMPDIR')
PWD = getcwd()
[home,SCIHOME]=sethomedirectory();
clear sethomedirectory ExistScilabHomeDirectory CreateScilabHomeDirectory
if MSDOS then
  SCI=strsubst(getshortpathname(SCI),'\','/');
  // path of scilab main directory for Windows
  WSCI=getlongpathname(pathconvert(SCI,%f,%f,'w'))
  clear pathconvert
else
  if getenv('PRINTERS','ndef')=="ndef" then
    setenv("PRINTERS","lp")
  end
end
setenv("VERSION",getversion())
// Set LANGUAGE  ======================================================
// used mainly for on-line help
global LANGUAGE
LANGUAGE="eng"
args=sciargs(); larg=find(args=="-l")
if larg<>[] & larg<=size(args,"*") then
  L=args(larg+1)
  if L=="eng" | L=="fr" then
    LANGUAGE=L
  else
    write(%io(2),[" "
		  "Unsupported language """+L+"""."+..
		  "Choosing default language """+LANGUAGE+"""."])
  end
end
clear  larg L

 exec("SCI/modules/helptools/etc/helptools.start",-1);
 
 exec("SCI/modules/linear_algebra/etc/linear_algebra.start",-1);
 exec("SCI/modules/elementaries_functions/etc/elementaries_functions.start",-1);
 exec("SCI/modules/gui/etc/gui.start",-1);
 exec("SCI/modules/statistics/etc/statistics.start",-1);
 exec("SCI/modules/cacsd/etc/cacsd.start",-1);
 exec("SCI/modules/signal_processing/etc/signal.start",-1);
 exec("SCI/modules/sparse/etc/sparse.start",-1);
 //exec("SCI/modules/arpack/etc/arpack.start",-1);
 exec("SCI/modules/polynomials/etc/polynomials.start",-1);
 exec("SCI/modules/wintools/etc/wintools.start",-1);
 exec("SCI/modules/string/etc/string.start",-1);
 exec("SCI/modules/optimization/etc/optimization.start",-1);
 exec("SCI/modules/scipad/etc/scipad.start",-1);
 exec("SCI/modules/maple2scilab/etc/maple.start",-1);
 exec("SCI/modules/m2sci/etc/m2sci.start",-1);
 exec("SCI/modules/graphics/etc/graphics.start",-1);
 exec("SCI/modules/Javasci/etc/javasci.start",-1);
 exec("SCI/modules/metanet/etc/metanet.start",-1);
 exec("SCI/modules/sound/etc/sound.start",-1);
 exec("SCI/modules/tclsci/etc/tclsci.start",-1);
 exec("SCI/modules/time/etc/time.start",-1);
 exec("SCI/modules/scicos/etc/scicos.start",-1);
 exec("SCI/modules/pvm/etc/pvm.start",-1);
 exec("SCI/modules/texmacs/etc/texmacs.start",-1);
 // Define Initial demo tables, ========================================
//demolist is a two column matrix of strings
global demolist
demolist=initial_demos_tables()
clear initial_demos_tables demolist



// Set the preferred browser  ==========================================
global %browsehelp
%browsehelp=loaddefaultbrowser();
clear %browsehelp with_tk with_gtk loaddefaultbrowser //remove the local variable
      
// Menu for Help and editor ===========================================
if grep(args,'scilex')<>[] then
  if (args<>"-nw")&(args<>"-nwni")&(args<>"--texmacs")&(args<>"-nogui") then
    
    if ~MSDOS then 
      delmenu("Help")
      addmenu("Help",["Help browser","Apropos","Configure"],list(2,"help_menu")),
    end
    if with_tk() then
	    if ~MSDOS then 
        delmenu("Editor")
        addmenu("Editor",list(2,"scipad();"));
      end
    end
  end
end
clear ans  %b_h_s args with_tk LANGUAGE delmenu addmenu

// LCC initialization =================================================
global LCC
if MSDOS then
  LCC=%f;
else
  LCC=%f
end
clear LCC 

// Protect variable previously defined  ================================
predef('all') 

// Graphic mode and Startup info ======================================
if %gui then 
  set old_style off
  verbose=sciargs()<>"-nb"
  if verbose then
	  show_startupinfo();
	  clear show_startupinfo;
  end
  clear verbose;
end  

// load contrib menu if present ========================================
[fd,ierr]=mopen(SCI+'/contrib/loader.sce');
if ierr== 0 then;
  mclose(fd); 
  global %toolboxes
  global %toolboxes_dir
  exec(SCI+'/contrib/loader.sce');
  clear %toolboxes %toolboxes_dir
end
clear fd ierr

// load history file ==================================================
if (sciargs()<>'-nouserstartup') & (sciargs()<>"-nwni") then
  if MSDOS then
    loadhistory(SCIHOME+'\history.scilab')
  else
    loadhistory(SCIHOME+'/.history.scilab')
  end
end


// Configure Environment Variables for Ms Visual C ====================
if MSDOS then
  configure_msvc()
  clear configure_msvc setmsvc71 setmsvc70 setmsvc60 setmsvc50
  clear setmsvc80std setmsvc80pro setmsvc80express
end

// calling user initialization =========================================
// Home dir startup (if any)
if sciargs()<>'-nouserstartup' then
  if MSDOS then
    startup_path=SCIHOME+'\'
  else
    startup_path=SCIHOME+'/'
  end

  [startup,ierr]=mopen(startup_path+'.scilab','r')
  if ierr==0 then
    exec(startup,-1);mclose(startup)
  else
    [startup,ierr]=mopen(startup_path+'scilab.ini','r')
    if ierr==0 then
      exec(startup,-1);mclose(startup)
    end
  end
  clear startup ierr startup_path

  // working dir startup (if any)
  if  home<>PWD then
    [startup,ierr]=mopen('.scilab','r')
    if ierr==0 then
      exec(startup,-1);mclose(startup)
    else
      [startup,ierr]=mopen('scilab.ini','r')
      if ierr==0 then
	exec(startup,-1);mclose(startup)
      end
    end
  end
end
clear startup ierr

