//
// Scilab ( http://www.scilab.org/ ) - This file is part of Scilab
// Copyright (C) 2008 - INRIA - Pierre MARECHAL
//
// This file is distributed under the same license as the Scilab package.
//

// =============================================================================
// BlackHole interface functions
// =============================================================================

// Step 1, set up the ini-position
// =============================================================================

function change_r()
	
	global g_r;
	global g_t;
	global g_V;
	global g_Vdir;
	
	g_r = get(findobj("tag", "pos_slider_1"),"value")/10;
	g_t = get(findobj("tag", "pos_slider_2"),"value");
	
	set(findobj("tag", "pos_value_1"),"string",string(g_r));
	
	draw_point(g_r,g_t,%F);
	
endfunction


function change_t()
	
	global g_r;
	global g_t;
	global g_V;
	global g_Vdir;
	
	g_r = get(findobj("tag", "pos_slider_1"),"value")/10;
	g_t = get(findobj("tag", "pos_slider_2"),"value");
	
	set(findobj("tag", "pos_value_2"),"string",string(g_t));
	draw_point(g_r,g_t,%F);
	
endfunction


function interrupt()
    global go_on fin;
    fin   = %T;
    go_on = %F;
endfunction


function position_next()
	
	global g_r;
	global g_t;
	global g_V;
	global g_Vdir;
	
	g_r = get(findobj("tag", "pos_slider_1"),"value")/10;
	g_t = get(findobj("tag", "pos_slider_2"),"value");
	
	obj_to_del = [        ..
		"pos_title_1"   ; ..
		"pos_text_1"    ; ..
		"pos_slider_1"  ; ..
		"pos_value_1"   ; ..
		"pos_unite_1"   ; ..
		"pos_text_2"    ; ..
		"pos_slider_2"  ; ..
		"pos_value_2"   ; ..
		"pos_unite_2"   ; ..
		"pos_button_1"  ; ..
		"pos_button_2"  ];
	
	my_figure_handle = scf(100001);
	my_figure_handle.immediate_drawing = "off";
	
	for i=1:size(obj_to_del,"*")
		obj = findobj("tag",obj_to_del(i));
		delete(obj);
	end
	
	my_figure_handle.immediate_drawing = "on";
	
	draw_speed(g_r,g_t,g_V,g_Vdir,%T);
	[g_V,g_Vdir,go_on]=speed();
	
endfunction


function [g_r,g_t,go_on] = position()
	
	
	my_figure_handle = scf(100001);
	
	axes_size_figure = my_figure_handle.axes_size;
	axes_figure_w    = axes_size_figure(1);
	axes_figure_h    = axes_size_figure(2);
	
	global go_on fin;
	
	frame_width  = 600;
	frame_pos_x  = 25;
	frame_pos_y  = 200;
	
	// Title
	// =========================================================================
	
	pos_title_1 = uicontrol(my_figure_handle                             , ...
		"position"            , [frame_pos_x+15 frame_pos_y-40 580 30]   , ...
		"Style"               , "text"                                   , ...
		"String"              , "Choose the starting point"              , ...
		"BackgroundColor"     , [1 1 1]                                  , ...
		"Fontsize"            , 16                                       , ...
		"horizontalalignment" , "center"                                 , ...
		"tag"                 , "pos_title_1"                            );
	
	// Slider 1
	// =========================================================================
	
	pos_text_1 = uicontrol(my_figure_handle                          , ...
		"position"          , [frame_pos_x frame_pos_y-60 40 15]     , ...
		"Style"             , "text"                                 , ...
		"String"            , "r"                                    , ...
		"BackgroundColor"   , [1 1 1]                                , ...
		"tag"               , "pos_text_1"                           );
	
	pos_slider_1 = uicontrol(my_figure_handle                        , ...
		"position"          , [frame_pos_x+50 frame_pos_y-60 450 15] , ...
		"Style"             , "slider"                               , ...
		"Value"             , g_r*10                                 , ...
		"SliderStep"        , [1 2]                                  , ...
		"Min"               , 5                                      , ...
		"Max"               , 13                                     , ...
		"callback"          , "change_r()"                           , ...
		"tag"               , "pos_slider_1"                         );
	
	pos_value_1 = uicontrol(my_figure_handle                         , ...
		"position"          , [frame_pos_x+520 frame_pos_y-60 30 15] , ...
		"Style"             , "text"                                 , ...
		"String"            , string(g_r)                            , ...
		"BackgroundColor"   , [1 1 1]                                , ...
		"tag"               , "pos_value_1"                          );
	
	pos_unite_1 = uicontrol(my_figure_handle                         , ...
		"position"          , [frame_pos_x+550 frame_pos_y-60 30 15] , ...
		"Style"             , "text"                                 , ...
		"String"            , "(m)"                                  , ...
		"BackgroundColor"   , [1 1 1]                                , ...
		"tag"               , "pos_unite_1"                          );
	
	// Slider 2
	// =========================================================================
	
	pos_text_2 = uicontrol(my_figure_handle                          , ...
		"position"       , [frame_pos_x frame_pos_y-80 40 15]        , ...
		"Style"          , "text"                                    , ...
		"String"         , "theta"                                   , ...
		"BackgroundColor", [1 1 1]                                   , ...
		"tag"            , "pos_text_2"                              );
	
	pos_slider_2 = uicontrol(my_figure_handle                        , ...
		"position"  , [frame_pos_x+50 frame_pos_y-80 450 15]         , ...
		"Style"     , "slider"                                       , ...
		"Min"       , 0                                              , ...
		"Max"       , 360                                            , ...
		"Value"     , g_t                                            , ...
		"SliderStep", [2 20]                                         , ...
		"callback"  , "change_t()"                                   , ...
		"tag"       , "pos_slider_2"                                 );
	
	pos_value_2 = uicontrol(my_figure_handle                         , ...
		"position"        , [frame_pos_x+520 frame_pos_y-80 30 15]   , ...
		"Style"           , "text"                                   , ...
		"String"          , string(g_t)                              , ...
		"BackgroundColor" , [1 1 1]                                  , ...
		"tag"             , "pos_value_2"                            );
	
	pos_unite_2 = uicontrol(my_figure_handle                         , ...
		"position"        , [frame_pos_x+550 frame_pos_y-80 30 15]   , ...
		"Style"           , "text"                                   , ...
		"String"          , "(deg)"                                  , ...
		"BackgroundColor" , [1 1 1]                                  , ...
		"tag"             , "pos_unite_2"                            );
	
	// Buttons
	// =========================================================================
	
	pos_button_1 = uicontrol(my_figure_handle                               , ...
		"Position"             , [frame_pos_x+150 frame_pos_y-120 100 20]   , ...
		"Style"                , "pushbutton"                               , ...
		"String"               , "cancel"                                   , ...
		"Fontsize"             , 14                                         , ...
		"BackgroundColor"      , [1 1 1]                                    , ...
		"callback"             , "interrupt();"                             , ...
		"tag"                  , "pos_button_1"                             );
	
	
	pos_button_2 = uicontrol(my_figure_handle                               , ...
		"Position"               , [frame_pos_x+300 frame_pos_y-120 100 20] , ...
		"Style"                  , "pushbutton"                             , ...
		"String"                 , "next"                                   , ...
		"Fontsize"               , 14                                       , ...
		"BackgroundColor"        , [1 1 1]                                  , ...
		"callback"               , "position_next();"                       , ...
		"tag"                    , "pos_button_2"                           );
	
	return
	
endfunction


// Step 2, set up the ini-speaborted
// =============================================================================

function change_V()
	
	global g_r;
	global g_t;
	global g_V;
	global g_Vdir;
	
	g_V    = get(findobj("tag", "spd_slider_1"),"value")/100;
	g_Vdir = get(findobj("tag", "spd_slider_2"),"value");
	set(findobj("tag", "spd_value_1"),"string",string(g_V));
	
	draw_speed(g_r,g_t,g_V,g_Vdir,%F);
	
endfunction


function change_Vdir()
	
	global g_r;
	global g_t;
	global g_V;
	global g_Vdir;
	
	g_V    = get(findobj("tag", "spd_slider_1"),"value")/100;
	g_Vdir = get(findobj("tag", "spd_slider_2"),"value");
	set(findobj("tag", "spd_value_2"),"string",string(g_Vdir));
	
	draw_speed(g_r,g_t,g_V,g_Vdir,%F);
	
endfunction


function speed_next()
	
	global g_r;
	global g_t;
	global g_V;
	global g_Vdir;
	
	g_V    = get(findobj("tag", "spd_slider_1"),"value")/100;
	g_Vdir = get(findobj("tag", "spd_slider_2"),"value");
	
	clear_ini();
	
	my_figure_handle = scf(100001);
	
	axes_size_figure = my_figure_handle.axes_size;
	axes_figure_w    = axes_size_figure(1);
	axes_figure_h    = axes_size_figure(2);
	
	global go_on fin;
	
	frame_width  = 300;
	frame_pos_x  = 25;
	frame_pos_y  = 200;
	
	obj_to_del = [        ..
		"spd_title_1"   ; ..
		"spd_text_1"    ; ..
		"spd_slider_1"  ; ..
		"spd_value_1"   ; ..
		"spd_unite_1"   ; ..
		"spd_text_2"    ; ..
		"spd_slider_2"  ; ..
		"spd_value_2"   ; ..
		"spd_unite_2"   ; ..
		"spd_button_1"  ; ..
		"spd_button_2"  ];
	
	my_figure_handle = scf(100001);
	my_figure_handle.immediate_drawing = "off";
	
	for i=1:size(obj_to_del,"*")
		obj = findobj("tag",obj_to_del(i));
		delete(obj);
	end
	
	my_figure_handle.immediate_drawing = "on";
	
	msg_gene_traj = uicontrol(my_figure_handle                             , ...
		"position"            , [frame_pos_x+15 frame_pos_y-40 580 30]   , ...
		"Style"               , "text"                                   , ...
		"String"              , "Generating trajectory"                   , ...
		"BackgroundColor"     , [1 1 1]                                  , ...
		"Fontsize"            , 16                                       , ...
		"horizontalalignment" , "center"                                 , ...
		"tag"                 , "msg_gene_traj"                            );
	
	tra = calculate_traj(g_r,g_t,g_V,g_Vdir);
	delete(msg_gene_traj);
	draw_traj_interface(tra);
	
endfunction


function [g_V,g_Vdir,go_on]=speed()
	
	my_figure_handle = scf(100001);
	
	axes_size_figure = my_figure_handle.axes_size;
	axes_figure_w    = axes_size_figure(1);
	axes_figure_h    = axes_size_figure(2);
	
	global go_on fin;
	
	frame_width  = 600;
	frame_pos_x  = 25;
	frame_pos_y  = 200;
	
	// Title
	// =========================================================================
	
	spd_title_1 = uicontrol(my_figure_handle                              , ...
		"Position"             , [frame_pos_x+15 frame_pos_y-40 580 30]   , ...
		"Style"                , "text"                                   , ...
		"String"               , "Choose the initial speed"               , ...
		"BackgroundColor"      , [1 1 1]                                  , ...
		"Fontsize"             , 16                                       , ...
		"horizontalalignment"  , "center"                                 , ...
		"tag"                  , "spd_title_1"                            );

	// Slider 1
	// =========================================================================
	
	spd_text_1 = uicontrol(my_figure_handle                    , ...
		"Position"             , [frame_pos_x frame_pos_y-60 40 15], ...
		"Style"                , "text"                        , ...
		"String"               , "speed"                       , ...
		"BackgroundColor"      , [1 1 1]                       , ...
		"tag"                  , "spd_text_1"                  );
		

	spd_slider_1 = uicontrol(my_figure_handle                  , ...
		"Position"             , [frame_pos_x+45 frame_pos_y-60 450 15], ...
		"Style"                , "slider"                      , ...
		"Min"                  , 0                             , ...
		"Max"                  , 200                           , ...
		"Value"                , g_V*100                       , ...
		"SliderStep"           , [5 20]                        , ...
		"callback"             , "change_V()"                  , ...
		"tag"                  , "spd_slider_1"                );

	spd_value_1 = uicontrol(my_figure_handle                   , ...
		"Position"             , [frame_pos_x+520 frame_pos_y-60 30 15], ...
		"Style"                , "text"                        , ...
		"String"               , string(round(100*g_V)/100)    , ...
		"BackgroundColor"      , [1 1 1]                       , ...
		"tag"                  , "spd_value_1"                  );


	spd_unite_1 = uicontrol(my_figure_handle                             , ...
		"Position"             , [frame_pos_x+550 frame_pos_y-60 30 15],...
		"Style"                , "text"                        ,...
		"String"               , "(m/s)"                       , ...
		"tag"                  , "spd_unite_1"                );
	
	// Slider 2
	// =========================================================================

	spd_text_2 = uicontrol(my_figure_handle                    , ...
		"Position"             , [frame_pos_x frame_pos_y-80 45 15] , ...
		"Style"                , "text"                        , ...
		"String"               , "direction"                   , ...
		"BackgroundColor"      , [1 1 1]                       , ...
		"tag"                  , "spd_text_2"                  );


	spd_slider_2 = uicontrol(my_figure_handle                  , ...
		"Position"             , [frame_pos_x+45 frame_pos_y-80 450 15], ...
		"Style"                , "slider"                      , ...
		"Min"                  , 0                             , ...
		"Max"                  , 360                           , ...
		"Value"                , g_Vdir                        , ...
		"SliderStep"           , [5 20]                        , ...
		"callback"             , "change_Vdir()"               , ...
		"tag"                  , "spd_slider_2"                );
		
		
	spd_value_2 = uicontrol(my_figure_handle                    , ...
		"Position"             , [frame_pos_x+520 frame_pos_y-80 30 15], ...
		"Style"                , "text"                        , ...
		"String"               , string(g_Vdir)                , ...
		"BackgroundColor"      , [1 1 1]                       , ...
		"tag"                  , "spd_value_2"                  );
	
	
	spd_unite_2 = uicontrol(my_figure_handle                  , ...
		"Position"             , [frame_pos_x+550 frame_pos_y-80 30 15], ...
		"Style"                , "text"                        , ...
		"String"               , "(deg)"                       , ...
		"BackgroundColor"      , [1 1 1]                       , ...
		"tag"                  , "spd_unite_2"                );
	
	
	// Buttons
	// =========================================================================
	
	spd_button_1 = uicontrol(my_figure_handle                  , ...
		"Position"             , [frame_pos_x+150 frame_pos_y-120 100 20] , ...
		"Style"                , "pushbutton"                  , ...
		"String"               , "cancel"                      , ...
		"Fontsize"             , 14                            , ...
		"BackgroundColor"      , [1 1 1]                       , ...
		"callback"             , "interrupt();"                , ...
		"tag"                  , "spd_button_1"                );
	
	spd_button_2 = uicontrol(my_figure_handle                  , ...
		"Position"               , [frame_pos_x+300 frame_pos_y-120 100 20] , ...
		"Style"                  , "pushbutton"                , ...
		"String"                 , "next"                      , ...
		"Fontsize"               , 14                          , ...
		"BackgroundColor"        , [1 1 1]                     , ...
		"callback"               , "speed_next();"             , ...
		"tag"                    , "spd_button_2"              );
		
	return
	
endfunction


// Step 3, launch
// =============================================================================

function change_alpha()
	
	global g_alpha;
	global g_theta;
	
	my_figure_handle = scf(100001);
	
	g_alpha = get(findobj("tag", "view_traj_slider_1"),"value");
	g_theta = get(findobj("tag", "view_traj_slider_2"),"value");
	
	set(findobj("tag","view_traj_value_1"),"string",string(g_alpha));
	
	curAxe                 = gca();
	curAxe.rotation_angles = [g_alpha,g_theta];
	
endfunction


function change_theta()

	global g_alpha;
	global g_theta;
	
	my_figure_handle = scf(100001);
	
	g_alpha = get(findobj("tag", "view_traj_slider_1"),"value");
	g_theta = get(findobj("tag", "view_traj_slider_2"),"value");
	
	set(findobj("tag", "view_traj_value_2"),"string",string(g_theta));
	
	curAxe                 = gca();
	curAxe.rotation_angles = [g_alpha,g_theta];
	
endfunction


function change_visibility()
	
	global eff;
	
	traj_val  = get(findobj("tag", "view_traj_checkbox_3"),"value");
	bille_val = get(findobj("tag", "view_traj_checkbox_4"),"value");
	
	if eff then
		
		my_figure_handle = scf(100001);
		my_figure_handle.immediate_drawing = "off";
		
		curAxe = gca();
		e2     = curAxe.children(1); //trajectory
		e1     = curAxe.children(2); //point
		
		if traj_val==1 then
			e1.visible="on";
		else
			e1.visible="off";
		end
		
		if bille_val==1 then
			e2.visible="on";
		else
			e2.visible="off";
		end
		
		my_figure_handle.immediate_drawing = "on";
	end
	
endfunction


function draw_traj_interface(Y)
	
	global eff;
	global g_alpha;
	global g_theta;
	global go_on fin;
	
	my_figure_handle = scf(100001);
	
	axes_size_figure = my_figure_handle.axes_size;
	axes_figure_w    = axes_size_figure(1);
	axes_figure_h    = axes_size_figure(2);
	
	global go_on fin;
	
	frame_width  = 600;
	frame_pos_x  = 25;
	frame_pos_y  = 200;
	
	// Title
	// =========================================================================
	
	view_traj_title1 = uicontrol(my_figure_handle                         , ...
		"Position"             , [frame_pos_x+15 frame_pos_y-40 600 30]   , ...
		"Style"                , "text"                                   , ...
		"String"               , "Change observation points"              , ...
		"horizontalalignment"  , "center"                                 , ...
		"BackgroundColor"      , [1 1 1]                                  , ...
		"Fontsize"             , 16                                       );
	
	// Slider 1 (change_alpha)
	// =========================================================================
	
	view_traj_text1 = uicontrol(my_figure_handle,             ...
		"Position"            , [frame_pos_x frame_pos_y-60 40 15]              , ...
		"Style"               , "text"                      , ...
		"String"              , "alpha"                     , ...
		"BackgroundColor"     ,[1 1 1]                      );
	
	view_traj_slider_1 = uicontrol(my_figure_handle,          ...
		"Position"            , [frame_pos_x+45 frame_pos_y-60 450 15]             , ...
		"Style"               , "slider"                    , ...
		"Min"                 , 1                           , ...
		"Max"                 , 90                          , ...
		"Value"               , g_alpha                     , ...
		"SliderStep"          , [5 10]                      , ...
		"callback"            , "change_alpha()"            , ...
		"tag"                 , "view_traj_slider_1"        );
		
	view_traj_value_1 = uicontrol(my_figure_handle          , ...
		"Position"            , [frame_pos_x+520 frame_pos_y-60 20 15]             , ...
		"Style"               , "text"                      , ...
		"String"              , string(g_alpha)             , ...
		"BackgroundColor"     , [1 1 1]                     , ...
		"tag"                 , "view_traj_value_1"          );
	
	view_traj_unite_1 = uicontrol(my_figure_handle          , ...
		"Position"          , [frame_pos_x+550 frame_pos_y-60 30 15]               , ...
		"Style"             , "text"                        , ...
		"String"            , "(deg)"                       , ...
		"BackgroundColor"   , [1 1 1]                       );
		
	// Slider 2 (change_theta)
	// =========================================================================
	
	view_traj_text2 = uicontrol(my_figure_handle,             ...
		"Position"            , [frame_pos_x frame_pos_y-80 45 15]              , ...
		"Style"               , "text"                      , ...
		"String"              , "theta"                     , ...
		"BackgroundColor"     ,[1 1 1]                      );


	view_traj_slider2 = uicontrol(my_figure_handle          , ...
		"Position"            , [frame_pos_x+45 frame_pos_y-80 450 15]              , ...
		"Style"               , "slider"                    , ...
		"Min"                 , 0                           , ...
		"Max"                 , 180                         , ...
		"Value"               , g_theta                     , ...
		"SliderStep"          , [5 20]                      , ...
		"callback"            , "change_theta()"            , ...
		"tag"                 , "view_traj_slider_2"        );
	
	view_traj_value_2 = uicontrol(my_figure_handle          , ...
		"Position"          , [frame_pos_x+520 frame_pos_y-80 20 15]                , ...
		"Style"             , "text"                        , ...
		"String"            , string(g_theta)               , ...
		"BackgroundColor"   , [1 1 1]                       , ...
		"tag"               , "view_traj_value_2"            );
	
	view_traj_unite_2 = uicontrol(my_figure_handle          , ...
		"Position"          , [frame_pos_x+550 frame_pos_y-80 30 15]                , ...
		"Style"             , "text"                        , ...
		"String"            , "(deg)"                       , ...
		"BackgroundColor",[1 1 1]                           );
	
	// Checkbox 3 (show trajectory)
	// =========================================================================
	
	view_traj_checkbox_3 = uicontrol(my_figure_handle                 , ...
		"Position"          , [frame_pos_x+150 frame_pos_y-110 15 15] , ...
		"Style"             , "checkbox"                              , ...
		"Min"               , 0                                       , ...
		"Max"               , 1                                       , ...
		"Value"             , 1                                       , ...
		"callback"          , "change_visibility()"                   , ...
		"tag"               , "view_traj_checkbox_3"                  );
	
	view_traj_text_3 = uicontrol(my_figure_handle                     , ...
		"Position"          , [frame_pos_x+50 frame_pos_y-110 80 15]  , ...
		"Style"             , "text"                                  , ...
		"String"            , "show trajectory"                        , ...
		"tag"               , "view_traj_text_3"                      , ...
		"BackgroundColor"   ,[1 1 1]                                  );
	
	// Checkbox 4 (show point)
	// =========================================================================
	
	view_traj_checkbox_4 = uicontrol(my_figure_handle                  , ...
		"Position"          , [frame_pos_x+400 frame_pos_y-110 15 15]  , ...
		"Style"             , "checkbox"                               , ...
		"Min"               , 0                                        , ...
		"Max"               , 1                                        , ...
		"Value"             , 0                                        , ...
		"callback"          , "change_visibility()"                    , ...
		"tag"               , "view_traj_checkbox_4"                   );
		
		
	view_traj_text_4 = uicontrol(my_figure_handle                      , ...
		"Position"          , [frame_pos_x+300 frame_pos_y-110 80 15]  , ...
		"Style"             , "text"                                   , ...
		"String"            , "show point"                             , ...
		"BackgroundColor"   ,[1 1 1]                                   );
	
	
	// Buttons ( interrupt -start - pause )
	// =========================================================================
	
	view_traj_button_5 = uicontrol(my_figure_handle                    , ...
		"Position"          , [frame_pos_x+50 frame_pos_y-150 100 20]   , ...
		"Style"             , "pushbutton"                             , ...
		"String"            , "stop"                                   , ...
		"Fontsize"          , 14                                       , ...
		"BackgroundColor"   , [1 1 1]                                  , ...
		"callback"          , "interrupt()"                            , ...
		"tag"               , "view_traj_button_5"                     );
	
	
	view_traj_button_6 = uicontrol(my_figure_handle                    , ...
		"Position"          , [frame_pos_x+250 frame_pos_y-150 100 20]  , ...
		"Style"             , "pushbutton"                             , ...
		"String"            , "start"                                  , ...
		"Fontsize"          , 14                                       , ...
		"BackgroundColor"   , [1 1 1]                                  , ...
		"callback"          , "m_start=%T"                             , ...
		"tag"               , "view_traj_button_6"                     );

	view_traj_button_7 = uicontrol(my_figure_handle                    , ...
		"Position"          , [frame_pos_x+450 frame_pos_y-150 100 20]   , ...
		"Style"             , "pushbutton"                             , ...
		"String"            , "pause"                                  , ...
		"Fontsize"          , 14                                       , ...
		"BackgroundColor"   ,[1 1 1]                                   , ...
		"callback"          , "m_start=%F"                             , ...
		"tag"               , "view_traj_button_7"                     );
		
		
	fin       = %F;
	m_start   = %F;
	step      = 0;
	r         =  0.1;
	[aux,num] = size(Y); //number of total steps
	g_tx      = Y(1,:)';
	g_ty      = Y(3,:)';
	g_tz      = -(ones(g_tx)./sqrt(g_tx^2+g_ty^2));
	
	while ~fin
		
		if ~m_start then
			sleep(1);
		else
			sleep(1);
			
			if (step<num) then
				
				my_figure_handle.immediate_drawing = "off";
				
				if (step == 0) then
					
					eff = %T;
					
					param3d(g_tx(1:3),g_ty(1:3),g_tz(1:3)+0.1)
					e1                 = gce();
					e1.thickness       = 2;
					e1.foreground      = 5;
					
					param3d(g_tx(3),g_ty(3),g_tz(3)+0.1);
					e2                 = gce();
					e2.mark_mode       = "on";
					e2.mark_style      = 0;
					e2.mark_size_unit  = "point";
					e2.mark_size       = 4;
					e2.mark_foreground = 6;
					
					change_visibility();
					
				else
					
					e1.data=[g_tx(1:step),g_ty(1:step),g_tz(1:step)+0.1];
					e2.data = [g_tx(step),g_ty(step),g_tz(step)+0.1];
					
				end
				
				my_figure_handle.immediate_drawing = "on";
				step = step +3;
				sleep(4)
			else
				fin   = %T;
				go_on = %F;
			end
		end
		
	end
	
	return

endfunction
